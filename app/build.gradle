apply plugin: 'com.android.application'
apply plugin: 'me.tatarka.retrolambda'
apply plugin: 'android-apt'

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.0"
    defaultConfig {
        applicationId 'jp.promin.android.blackhistory'

        minSdkVersion 19
        targetSdkVersion 25

        versionCode 3
        versionName '3ページ目'
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    lintOptions{
        checkReleaseBuilds false
        abortOnError false
        check 'NewApi', 'InlinedApi'
        ignoreWarnings true
    }

    dataBinding {
        enabled = true
    }

    packagingOptions {
        exclude 'META-INF/services/javax.annotation.processing.Processor'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
    }

    buildTypes{
        applicationVariants.all { variant ->
            variant.outputs.each { output ->
                output.outputFile = new File(
                        output.outputFile.parent,
                        output.outputFile.name.replace(".apk", "-${variant.versionName}-${variant.versionCode}.apk"))
            }
        }
        release {
            minifyEnabled true
            proguardFiles 'proguard-rules.pro'
        }
        debug {
            minifyEnabled false
            proguardFiles 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    configurations.all {
        resolutionStrategy.force 'com.google.code.findbugs:jsr305:1.3.9'
    }
}

repositories {
    mavenCentral()
}

dependencies {

    compile 'com.android.support:support-v4:25.0.1'
    compile 'com.android.support:appcompat-v7:25.0.1'
    compile 'com.android.support:design:25.0.1'

    // Twitter
    compile 'org.twitter4j:twitter4j-http2-support:4.0.5'
    compile 'org.twitter4j:twitter4j-stream:4.0.5'

    // Rx
    compile 'io.reactivex:rxandroid:1.2.1'
    compile 'com.trello:rxlifecycle-components:1.0'

    compile 'com.jakewharton:butterknife:7.0.1'

    // PermissionDispatcher
    compile 'com.github.hotchemi:permissionsdispatcher:2.2.0'
    apt 'com.github.hotchemi:permissionsdispatcher-processor:2.2.0'

    compile 'com.jakewharton.picasso:picasso2-okhttp3-downloader:1.1.0'

    compile 'io.realm:realm-android:0.87.5'

    // will remove
    compile "com.squareup:otto:1.3.8"

    //********** Test
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    testCompile 'junit:junit:4.12'
}

apply from: '../tasks/findbugs.gradle'
check.dependsOn 'findbugs'
